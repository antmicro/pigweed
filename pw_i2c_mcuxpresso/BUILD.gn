# Copyright 2022 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.
import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_third_party/mcuxpresso/mcuxpresso.gni")
import("$dir_pw_unit_test/test.gni")

config("default_config") {
  include_dirs = [ "public" ]
}

if (pw_third_party_mcuxpresso_SDK != "") {
  pw_source_set("pw_i2c_mcuxpresso") {
    public_configs = [ ":default_config" ]
    public = [ "public/pw_i2c_mcuxpresso/initiator.h" ]
    public_deps = [
      "$dir_pw_chrono:system_clock",
      "$dir_pw_i2c:initiator",
      "$dir_pw_status",
      "$dir_pw_sync:interrupt_spin_lock",
      "$dir_pw_sync:lock_annotations",
      "$dir_pw_sync:mutex",
      "$dir_pw_sync:timed_thread_notification",
      "$pw_third_party_mcuxpresso_SDK",
    ]
    sources = [ "initiator.cc" ]
  }

  pw_source_set("i3c_initiator") {
    public_configs = [ ":default_config" ]
    public = [
      "public/pw_i2c_mcuxpresso/i3c_ccc.h",
      "public/pw_i2c_mcuxpresso/i3c_initiator.h",
    ]
    public_deps = [
      "$dir_pw_chrono:system_clock",
      "$dir_pw_i2c:initiator",
      "$dir_pw_status",
      "$dir_pw_sync:interrupt_spin_lock",
      "$dir_pw_sync:lock_annotations",
      "$dir_pw_sync:mutex",
      "$dir_pw_sync:timed_thread_notification",
      "$pw_third_party_mcuxpresso_SDK",
    ]
    sources = [ "i3c_initiator.cc" ]
  }
}

pw_test("initiator_test") {
  enable_if =
      pw_third_party_mcuxpresso_SDK == "$dir_pigweed/targets/mimxrt595_evk_freertos:sdk" &&
      (pw_toolchain_SCOPE.name == "mimxrt595_evk_freertos_debug" ||
       pw_toolchain_SCOPE.name == "mimxrt595_evk_freertos_size_optimized" ||
       pw_toolchain_SCOPE.name == "mimxrt595_evk_freertos_speed_optimized")
  sources = [ "initiator_test.cc" ]
  deps = [
    ":pw_i2c_mcuxpresso",
    "$dir_pigweed/targets/mimxrt595_evk_freertos:sdk",
  ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}

pw_test_group("tests") {
  tests = [ ":initiator_test" ]
}
